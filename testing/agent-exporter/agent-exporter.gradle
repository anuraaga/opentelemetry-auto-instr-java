plugins {
  id "com.github.johnrengelman.shadow"
}

apply from: "$rootDir/gradle/java.gradle"

dependencies {
  compileOnly(project(':javaagent-spi'))
  compileOnly(deps.bytebuddy)
  compileOnly(deps.opentelemetrySdk)
  compileOnly(deps.slf4j)

  annotationProcessor deps.autoservice
  compileOnly deps.autoservice

  implementation project(':javaagent-tooling')
  implementation deps.opentelemetryProto
  implementation deps.opentelemetryOtlp
  implementation group: 'io.grpc', name: 'grpc-testing', version: '1.30.2'

  // Include instrumentations instrumenting core JDK classes tp ensure interoperability with other instrumentation
  implementation project(':instrumentation:java-concurrent')
  // FIXME: we should enable this, but currently this fails tests for google http client
  //testImplementation project(':instrumentation:http-url-connection')
  implementation project(':instrumentation:java-classloader')
}

jar.enabled = false
shadowJar {
  archiveClassifier = ''

  mergeServiceFiles()

  exclude '**/module-info.class'

  // Prevents conflict with other SLF4J instances. Important for premain.
  relocate 'org.slf4j', 'io.opentelemetry.javaagent.slf4j'
  // rewrite dependencies calling Logger.getLogger
  relocate 'java.util.logging.Logger', 'io.opentelemetry.javaagent.bootstrap.PatchLogger'

  // relocate OpenTelemetry API usage
  relocate "io.opentelemetry.api", "io.opentelemetry.javaagent.shaded.io.opentelemetry.api"
  relocate "io.opentelemetry.context", "io.opentelemetry.javaagent.shaded.io.opentelemetry.context"
}