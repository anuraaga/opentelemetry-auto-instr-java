plugins {
  id "com.github.johnrengelman.shadow"
}

apply from: "$rootDir/gradle/java.gradle"

jar {
  manifest {
    attributes(
      "Main-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
      "Agent-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
      "Premain-Class": "io.opentelemetry.javaagent.OpenTelemetryAgent",
      "Can-Redefine-Classes": true,
      "Can-Retransform-Classes": true,
    )
  }
}

CopySpec isolateSpec(Collection<Jar> shadowJarTasks) {
  return copySpec {
    from({ shadowJarTasks.collect { zipTree(it.archiveFile) } }) {
      // important to keep prefix 'inst' short, as it is prefixed to lots of strings in runtime mem
      into 'inst'
      exclude 'io/opentelemetry/javaagent/instrumentation/api/**/*.class'
      rename '(^.*)\\.class$', '$1.classdata'
      // Rename LICENSE file since it clashes with license dir on non-case sensitive FSs (i.e. Mac)
      rename '^LICENSE$', 'LICENSE.renamed'
    }
    //This allows every instrumentation to have their own classes that should go to bootstrap classloader
    from({ shadowJarTasks.collect { zipTree(it.archiveFile) } }) {
      include 'io/opentelemetry/javaagent/instrumentation/api/**/*.class'
    }
  }
}

configurations {
  shadowInclude
}

shadowJar {
  configurations = [project.configurations.shadowInclude]
  archiveFileName = 'javaagent-for-testing.jar'

  dependsOn ':testing:agent-exporter:shadowJar'
  with isolateSpec([project(':testing:agent-exporter').tasks.shadowJar])

  manifest {
    inheritFrom project.tasks.jar.manifest
  }

  mergeServiceFiles()

  exclude '**/module-info.class'

  // Prevents conflict with other SLF4J instances. Important for premain.
  relocate 'org.slf4j', 'io.opentelemetry.javaagent.slf4j'
  // rewrite dependencies calling Logger.getLogger
  relocate 'java.util.logging.Logger', 'io.opentelemetry.javaagent.bootstrap.PatchLogger'

  // prevents conflict with library instrumentation
  relocate 'io.opentelemetry.instrumentation.api', 'io.opentelemetry.javaagent.shaded.instrumentation.api'

  // relocate OpenTelemetry API
  relocate "io.opentelemetry.api", "io.opentelemetry.javaagent.shaded.io.opentelemetry.api"
  relocate "io.opentelemetry.spi", "io.opentelemetry.javaagent.shaded.io.opentelemetry.spi"
  relocate "io.opentelemetry.context", "io.opentelemetry.javaagent.shaded.io.opentelemetry.context"

  // this is for instrumentation on opentelemetry-api itself
  relocate "application.io.opentelemetry", "io.opentelemetry"
}

dependencies {
  // Dependencies to include without obfuscation.
  shadowInclude project(':javaagent-bootstrap')

  testImplementation project(':testing-common')
  testImplementation deps.opentelemetryApi
}

tasks.withType(Test).configureEach {
  jvmArgs "-Dotel.javaagent.debug=true"
  // the debug level logging in this class occurs frequently (and under class file transform),
  // which can lead gradle to deadlock sporadically when it triggers a class to load while holding
  // a lock, and then (because gradle hijacks System.out), it gets called from inside of the class
  // file transform, and then tries to grab a different lock
  jvmArgs "-Dio.opentelemetry.javaagent.slf4j.simpleLogger.log.io.opentelemetry.javaagent.tooling.context.FieldBackedProvider=info"
  jvmArgs "-javaagent:${shadowJar.archiveFile.get().asFile.absolutePath}"

  dependsOn shadowJar
}
